allprojects {
    apply plugin: "java"
    apply plugin: "maven-publish"

    archivesBaseName = rootProject.archives_base_name
    version = rootProject.mod_version
    group = rootProject.maven_group

    repositories {
        // Add repositories to retrieve artifacts from in here.
        // You should only use this when depending on other mods because
        // Loom adds the essential maven repositories to download Minecraft and libraries from automatically.
        // See https://docs.gradle.org/current/userguide/declaring_repositories.html
        // for more information about repositories.
    }

    tasks.withType(JavaCompile) {
        options.encoding = "UTF-8"
        options.release = 17
    }

    java {
        withSourcesJar()
    }
}


['1.16.5','1.18.2','1.19.2','1.19.4','1.20.1'].forEach {ver->
    ['forge','fabric'].forEach {api->
        tasks.register("runClient-${ver}-${api}"){
            dependsOn(":${ver}:${api}:runClient")
        }
        tasks.register("build-jar-${ver}-${api}",Copy){
            dependsOn(":${ver}:${api}:build")
            with{
                from("./${ver}/${api}/build/libs/yuushya-${mod_version}.jar")
                rename {"yuushya-${ver}-${api}-${mod_version}.jar"}
                into("./build/libs")
                duplicatesStrategy(DuplicatesStrategy.INHERIT)
            }
        }
    }
}

tasks.register("build-jar"){
    ['1.16.5','1.18.2','1.19.2','1.19.4','1.20.1'].forEach {ver->
        ['forge','fabric'].forEach { api ->
            dependsOn "build-jar-${ver}-${api}"
        }
    }
}